<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:local="*"
					   width="1024" height="768" creationComplete="onInit(event)">
	
	<fx:Script>
		<![CDATA[
			import com.swfdiy.data.ABC;
			import com.swfdiy.data.SWF;
			import com.swfdiy.data.SWFTag;
			import com.swfdiy.data.SWFTag.TagDoABC;
			import com.swfdiy.data.SWFTag.TagEnd;
			
			import mx.collections.ArrayCollection;
			import mx.events.ItemClickEvent;
			import mx.events.ListEvent;
			
			import spark.events.TextOperationEvent;
			
			[Bindable]
			[Embed(source="icon.jpg")] 
			public var iconSymbol1:Class; 
			
			[Bindable]
			private var scriptFiles:Object ;
			
			private var swf:SWF;
			private var abcs:Array;
			private var swfTags:Array;
			private var scripts:Array;
			private var abcPages:Object;
			public var showingABCPage:ABCPage;
			
			private var abcListData:ArrayCollection;
			
			private function onInit(e:Event):void {
				//scriptFiles = new ArrayCollection();
				//var b:ArrayCollection =  new ArrayCollection();
				//scriptFiles.addItem({label:"wori", children:[{label:"hei"}]});
				//scriptFiles.addItem({label:"wori2"});
				//scriptFiles.addItem(b);
				this.addEventListener("close_abcpage", onCloseABCPage);
				abcList.addEventListener('rendererDoubleClick', onItemDoubleClick);
				
				start_group.visible = true;
				abcPageContainer.visible = false;
			}
			
			protected function onOpenSWF(event:MouseEvent):void
			{
				var allFilter:FileFilter = new FileFilter("swf (*.swf)", "*.swf");
				
				Utils.openAndReadFile(_onClickOpen, allFilter);
			}	
			
			private function _onClickOpen(data:Array, files:Array):void {			
				swf = new SWF(data[0]);
				abcs = [];
				scripts = [];
				swf.startReadTags();	
				swfTags = [];
				abcPages = {};
				
				while(1) {
					var start_pos:int = swf.pos;
					var tag:SWFTag = swf.read_tag() ;
					
					if (tag == null) {
						break;
					}
					
					
					if (tag is TagDoABC) {
						var tagDoABC:TagDoABC =  tag as TagDoABC;
						
						abcs.push(new ABCWrapper(tagDoABC));						
					}
					
					swfTags.push(tag);
				}
				
				var i:int;
				var j:int;
				
				scriptFiles = {children:[], label: files[0]};
				
				abcListData = new ArrayCollection;
				for (i=0;i<abcs.length;i++) {
					var abc:ABCWrapper = abcs[i];
					var name:String = abc.getName();
					if (name == "") {
						name = "ABC" + i;
					}
					abcListData.addItem({ value: i, label: name});
					/*for (j=0;j<abc.scripts.length;j++) {
						var script:ScriptWrapper = abc.scripts[j];
						var sn:Array = script.getScriptName();
						if (sn.length <= 0) {
							continue;
						}
						scripts.push(script);
						addFile(sn, scripts.length-1);
					}*/
				}
				
				abcList.dataProvider =abcListData;
				
			}
			
			protected function onSaveSWF(event:MouseEvent):void
			{
				// TODO Auto-generated method stub
				var i:int;
				var abci:int;
				for (i=0,abci=0;i<swfTags.length;i++) {
					if (swfTags[i] is TagDoABC) {
						var innerabc:ABC = abcs[abci].getABC();
						if (innerabc.changed) {
							innerabc.refreshRawData();
							
							//refresh the tag
							TagDoABC(swfTags[i]).refreshRawData();
						}
						
						
						//if changed
						//TagDoABC(swfTags[i]).updateAbc(abcs[abci].getABC());
						
						//innerabc.refreshRawData();
						abci++;
					}
				}
				var new_swf_bytes:ByteArray = swf.make_swf_bytes_from_tags(swfTags, true);
				var f:FileReference = new FileReference;
				f.save(new_swf_bytes, "new.swf");
				
			}
			
			protected function onItemDoubleClick(event:MyEvent):void
			{
				trace(event.data);
				var i:int = int(event.data);
				
				if (abcPages[i]) {
					showABCPage(i);
				} else {
					var abcPage:ABCPage = new ABCPage();
					abcPage.init(abcs[i], i);
					abcPages[i] = abcPage;
					showABCPage(i);
				}
			}
			
			private function showABCPage(showIndex:int):void
			{
				// TODO Auto Generated method stub
				/*for (var i:String in abcPages) {
					abcPages[i].visible = false;
				}*/
				if (showingABCPage) {
					showingABCPage.visible = false;
					showingABCPage = null;
				}
				abcPages[showIndex].visible = true;
				//abcPages[showIndex].x = 
				abcPageContainer.addElement(abcPages[showIndex]);
				showingABCPage = abcPages[showIndex];
				start_group.visible = false;
				abcPageContainer.visible = true;
			}
			
			
			
			protected function onCloseABCPage(event:Event):void
			{
				start_group.visible = true;
				abcPageContainer.visible = false;
			}
			
			private function filterMyArrayCollection(item:Object):Boolean {
				var searchString:String = searchTxt.text.toLowerCase();
				var itemName:String = (item.label as String).toLowerCase();
				return itemName.indexOf(searchString) > -1;
			}
			
			
			protected function searchTxt_changeHandler(event:TextOperationEvent):void
			{
				abcListData.filterFunction = filterMyArrayCollection;
				abcListData.refresh();
			}
			
			
			
			protected function onSaveABCTag(event:MouseEvent):void
			{
				// TODO Auto-generated method stub
				var i:int = abcList.selectedIndex;
				if (i<0) {
					return;
				}
				var item:ABCWrapper = ABCWrapper(abcs[abcList.selectedItem.value]);
				var tag:TagDoABC = item.getTag();
				
				var innerabc:ABC = item.getABC();
				if (innerabc.changed) {
					innerabc.refreshRawData();
					
					tag.refreshRawData();
				}
				var new_swf_bytes:ByteArray = tag.tagData();
				var f:FileReference = new FileReference;
				f.save(new_swf_bytes, abcList.selectedItem.label + ".abc.tag");
			}
			
			protected function onInsertTag(event:MouseEvent):void
			{
				// TODO Auto-generated method stub
				var allFilter:FileFilter = new FileFilter("tag (*.tag)", "*.tag");
				
				Utils.openAndReadFile(_onClickInsert, allFilter);
			}
			
		
			
			private function _onClickInsert(data:Array, files:Array):void {	
				var ba:ByteArray = data[0];
				var tag:SWFTag = SWF.read_tag_from_bytes(ba);
				if (tag) {
					insertTag(tag);
					if (tag is TagDoABC) {
						//insert 
						var abcWrapper:ABCWrapper = new ABCWrapper(TagDoABC(tag));
						abcs.push(abcWrapper);
						
						var name:String = abcWrapper.getName();
						if (name == "") {
							name = "ABC" + (abcs.length-1);
						}
						abcListData.addItem({ value: abcs.length-1, label: name});
					}
				}
				
			}
			
			private function insertTag(tag:SWFTag):void {
				var i:int;
				//find end tag
				var newTags:Array = [];
				var inserted:Boolean = false;
				for (i=0;i<swfTags.length;i++) {
					
					if (swfTags[i] is TagEnd) {
						newTags.push(tag);
						inserted = true;
					}
					
					newTags.push(swfTags[i]);
				}
				
				if (!inserted) {
					newTags.push(tag);
				}
				
				swfTags = newTags;
			}
				
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	


	<s:Group id="start_group" width="468" height="428">
		<s:Button x="16" y="28" label="openSWF" click="onOpenSWF(event)" height="40" width="85"/>
		<s:Button x="126" y="29" label="saveSWF" click="onSaveSWF(event)" height="40" width="85"/>
		<s:Button x="223" y="28" label="saveABCTag" click="onSaveABCTag(event)" height="40" width="98"/>
		<s:Button x="327" y="28" label="insertTag" click="onInsertTag(event)" height="40" width="98"/>
		<s:List id="abcList" x="11" y="122" width="310" height="288"  >
			<s:itemRenderer>
				<fx:Component>
					<s:ItemRenderer doubleClick="handleDoubleClick()" doubleClickEnabled="true">
						<fx:Script>
							<![CDATA[
								import spark.components.List;
								private function handleDoubleClick():void {
									var e:MyEvent = new MyEvent("rendererDoubleClick");
									e.data = data.value;
									owner.dispatchEvent(e);
								}
							]]>
						</fx:Script>
						<s:Label text="{data.label}" top="5" bottom="5" right="3" left="3" />
					</s:ItemRenderer>
				</fx:Component>
			</s:itemRenderer>
			
		</s:List>
		<s:Label x="15" y="76" width="209" height="12" text="double click to open ABC"/>
		<s:TextInput  x="11" y="92" width="310" id="searchTxt" change="searchTxt_changeHandler(event)"/>
	</s:Group>
	<s:Group id="abcPageContainer" x="0" y="0" width="1024" height="768" >
	
	</s:Group>
</s:WindowedApplication>
